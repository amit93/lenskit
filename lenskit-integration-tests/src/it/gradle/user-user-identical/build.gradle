buildscript {
    repositories {
        maven {
            url project.testRepoURI
        }
        mavenCentral()
    }
    dependencies {
        classpath "org.grouplens.lenskit:lenskit-gradle:$project.lenskitVersion"
    }
}

import org.lenskit.gradle.*

apply plugin: 'java'
apply plugin: 'lenskit'
apply from: 'common.gradle'
apply from: 'renjin.gradle'

dependencies {
    // use a separate 'analyze' classpath b/c of Renjin compat
    analyze 'org.hamcrest:hamcrest-library:1.3'
    analyze 'com.xlson.groovycsv:groovycsv:1.0'
}

task crossfold(type: Crossfold) {
    invoker.enableAssertions = true
    input textFile {
        file ratingsFile
        delimiter "\t"
    }
    outputFormat "PACK"
    partitionCount 5
    userPartitionMethod holdout(5)
    includeTimestamps false
}

task trainTest(type: TrainTest) {
    invoker.enableAssertions = true
    maxMemory '256m'
    dataSet crossfold
    cacheDirectory 'cache'
    outputFile 'results.csv'
    userOutputFile 'users.csv'
    algorithm 'algorithms.groovy'
    predict {
        outputFile 'predictions.csv'
        metric 'coverage'
        metric 'rmse'
    }
}

task checkGroovy(type: JavaExec) {
    dependsOn trainTest
    classpath configurations.analyze
    enableAssertions true

    main 'groovy.ui.GroovyMain'
    args 'verify.groovy'
}

check {
    dependsOn checkGroovy
}
